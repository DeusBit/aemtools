buildscript {
    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
        maven { url 'http://dl.bintray.com/jetbrains/intellij-plugin-service' }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.2.17"
    id "io.gitlab.arturbosch.detekt" version "1.0.0.RC4-3"
}

apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'jacoco'

apply from: rootProject.file('config/jacoco.gradle')

sourceCompatibility = javaVersion
targetCompatibility = javaVersion
compileKotlin.kotlinOptions.jvmTarget = javaVersion
compileTestKotlin.kotlinOptions.jvmTarget = javaVersion

tasks.withType(JavaCompile) { options.encoding = 'UTF-8' }

// take the version number defined in 'gradle.properties' and use that in plugin.xml
version = "${version}"
task initConfig(type: Copy) {
    from('src/main/resources') {
        include '**/plugin.xml'
        filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [version: version])
    }
}

apply plugin: 'org.jetbrains.intellij'
intellij {
    pluginName 'aemtools'
    version ideaVersion
    updateSinceUntilBuild = false

    // SET YOUR JETBRAINS LOGIN DETAILS AS ENVIRONMENT VARIABLES SO THAT YOU CAN PUBLISH A NEW BUILD TO THE REPOSITORY
    publishPlugin {
        username System.getenv('JETBRAINS_USERNAME')
        password System.getenv('JETBRAINS_PASSWORD')
    }
}

sourceSets {
    main.java.srcDirs += "src/main/gen"
    main.kotlin.srcDirs += "src/main/kotlin"
    main.resources.srcDir "src/main/resources"

    test.kotlin.srcDir 'src/test/kotlin'
    test.resources.srcDirs += "src/main/resources"
}

test {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat = 'full'
    }
}

configurations {
    gen
}

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-runtime:$kotlin_version"

    compile "com.google.code.gson:gson:$gson_version"

    compile "commons-lang:commons-lang:$apache_commons_version"

    gen files('lib/grammar-kit-2.jar')

    gen 'de.jflex:jflex:1.6.1'

    // https://mvnrepository.com/artifact/com.github.markusbernhardt/proxy-vole
    testCompile group: 'com.github.markusbernhardt', name: 'proxy-vole', version: '1.0.3'

    testCompile "junit:junit:$junit_version"

    testCompile "org.jmockit:jmockit:$jmockit_version"

    testCompile "org.assertj:assertj-core:$assertj_version"
    testCompile "org.mockito:mockito-core:$mockito_version"
}

apply from: rootProject.file("config/idea.gradle")

/**
 * Import Lexer Parser Related tasks.
 */
apply from: rootProject.file("config/lexerparser.gradle")

apply from: rootProject.file("config/detekt.gradle")

task wrapper(type: Wrapper) {
    gradleVersion = '4.2.1'
}
